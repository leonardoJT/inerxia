  DEFINE INPUT PARAMETER P_Monto    AS   DECIMAL FORMAT "->>>,>>>,>>>,>>>,>>9". /*LIKE Asesoria.Valor.*/
  DEFINE INPUT PARAMETER P_Plazo    AS   INTEGER FORMAT "->>>>9" .      /*LIKE Asesoria.Plazo.*/
  DEFINE INPUT PARAMETER P_Cuota    AS   DECIMAL FORMAT "->>>,>>>,>>>,>>>,>>9". /*LIKE Asesoria.Cuota.*/
  DEFINE INPUT PARAMETER P_TotExt   AS   DECIMAL FORMAT "->>>,>>>,>>>,>>>,>>9".
  DEFINE INPUT PARAMETER P_Fecha    LIKE Solicitud.Fec_Solicitud.
  DEFINE INPUT PARAMETER P_Tasa     LIKE Solicitud.Tasa FORMAT "999.9999999".
  DEFINE INPUT PARAMETER P_Razon    LIKE Solicitud.Incremento.
  DEFINE INPUT PARAMETER P_Gracia   LIKE Solicitud.Per_Gracia.
  DEFINE INPUT PARAMETER P_PerDed   AS   INTEGER.
  DEFINE INPUT PARAMETER P_TipInt   AS   INTEGER.
  DEFINE INPUT PARAMETER P_Sistema  LIKE Solicitud.Sistema.
  DEFINE INPUT PARAMETER P_Nit      LIKE Solicitud.Nit.
  DEFINE INPUT PARAMETER P_Nombre   AS   CHAR FORMAT "X(40)".
  DEFINE INPUT PARAMETER P_CodPro   LIKE Solicitud.Cod_Credito.
  DEFINE INPUT PARAMETER P_NomPro   AS   CHAR FORMAT "X(40)". 
  DEFINE INPUT PARAMETER P_TipArc   AS   CHAR.
  DEFINE INPUT PARAMETER P_NroDcto  LIKE Solicitud.Num_Solicitud.
  DEFINE INPUT PARAMETER P_NomStma  AS   CHAR FORMAT "X(40)".
  DEFINE INPUT PARAMETER P_NomPer   AS   CHAR FORMAT "X(14)".
  DEFINE INPUT PARAMETER P_RForma   AS   INTEGER.
  DEFINE INPUT PARAMETER P_VrSoli   LIKE Solicitud.Monto.
  
  DEFINE SHARED VAR W_ManFin      AS   HANDLE.
  DEFINE SHARED VAR W_Nom_Entidad LIKE Entidad.Nombre.
  DEFINE SHARED VAR W_Nom_Agencia LIKE Agencias.Nombre.
  DEFINE SHARED VAR W_Usuario     LIKE Usuario.Usuario.
  DEFINE SHARED VAR W_Agencia     LIKE Agencias.Agencia.  
  
  DEFINE VAR W_Ubicacion  AS CHARACTER FORMAT "X(40)".
  DEFINE VAR W_VlrInteres LIKE Creditos.Cuota INITIAL 0.
  DEFINE VAR W_NomUsu     LIKE Usuarios.Nombre.
  DEFINE VAR W_PerTra    AS INTEGER INITIAL 0.
  DEFINE VAR W_NroDias   AS INTEGER INITIAL 0. 
  DEFINE VAR W_SdoCapTra AS DECIMAL INITIAL 0 FORMAT "->>>,>>>,>>>,>>>,>>9".  
  DEFINE VAR W_CuoTra    AS DECIMAL INITIAL 0 FORMAT "->>>,>>>,>>>,>>>,>>9".
  DEFINE VAR W_VlrPte    AS DECIMAL INITIAL 0 FORMAT "->>>,>>>,>>>,>>>,>>9".
  DEFINE VAR W_TvrPte    AS DECIMAL INITIAL 0 FORMAT "->>>,>>>,>>>,>>>,>>9".
  DEFINE VAR W_FecTra    AS DATE    FORMAT "99/99/9999".
  DEFINE VAR W_VlrExt    AS DECIMAL INITIAL 0.
  DEFINE VAR W_TasTra    AS CHAR    FORMAT "X(40)" INITIAL "".
  DEFINE VAR W_MonTra    AS CHAR    FORMAT "X(20)" INITIAL "".
  DEFINE VAR W_RazTra    AS CHAR    FORMAT "X(20)" INITIAL "".
  DEFINE VAR W_Anualidad AS CHAR    FORMAT "X(20)" INITIAL "".
  DEFINE VAR W_NroPer    AS INTEGER INITIAL 0.
  DEFINE VAR I           AS INTEGER INITIAL 0.
  DEFINE VAR W_TitP      AS CHAR    FORMAT "X(16)" INITIAL "Nro.Solicitud  :".
  DEFI   VAR INT_MGracia LIKE Creditos.Cuota INITIAL 0.   /*Valor c/mes intereses del Pdo-Gracia*/
  DEFI   VAR W_TotGra    LIKE Creditos.Cuota INITIAL 0.

  DEFINE VAR W_Primero   AS LOGICAL INITIAL YES.
  DEFINE VAR W_RegPri    AS INTEGER FORMAT "9".
  DEFI   VAR W_FecIni    LIKE Solicitud.Fec_Solicitud.
  DEFI   VAR W_FecChar   AS CHAR FORM "X(10)".
  DEFI   VAR W_Si28      AS LOG INIT FALSE.
  DEFI   VAR W_Si29      AS LOG INIT FALSE.
           
  DEFINE TEMP-TABLE W_TabAmor
     FIELD W_Periodo AS INTEGER INITIAL 0
     FIELD W_Fecha   AS DATE    FORMAT "99/99/9999"
     FIELD W_Cuota   AS DECIMAL INITIAL 0 FORMAT "->>>,>>>,>>>,>>>,>>9"
     FIELD W_AboCap  AS DECIMAL INITIAL 0 FORMAT "->>>,>>>,>>>,>>>,>>9"
     FIELD W_AboInt  AS DECIMAL INITIAL 0 FORMAT "->>>,>>>,>>>,>>>,>>9"
     FIELD W_SdoCap  AS DECIMAL INITIAL 0 FORMAT "->>>,>>>,>>>,>>>,>>9".

  DEFINE TEMP-TABLE W_TabExt
     FIELD W_Fecha   AS DATE    FORMAT "99/99/9999"
     FIELD W_PlazExt LIKE Solicitud.Plazo
     FIELD W_Cuota   AS DECIMAL INITIAL 0 FORMAT "->>>,>>>,>>>,>>>,>>9".

  DEFINE VAR W_TotInt  AS DECIMAL INITIAL 0 FORMAT "->>>,>>>,>>>,>>>,>>9".
  DEFINE VAR W_TotCap  AS DECIMAL INITIAL 0 FORMAT "->>>,>>>,>>>,>>>,>>9".
  DEFINE VAR W_TDeduc  LIKE Pro_Creditos.Deducible.
  DEFINE VAR W_VDeduc  LIKE Solicitud.Deducible.
  DEFINE VAR W_IDeduc  LIKE Solicitud.Id_Adicionales.
  DEFINE VAR W_MDeduc  LIKE Solicitud.Monto.

  RUN RUTFINAN.R PERSISTENT SET W_MANFIN.

    IF P_Perded EQ 1 THEN                               /* 1. semanal       */
       ASSIGN W_NroDias =  7
              W_NroPer  = 52.
    ELSE IF P_Perded EQ 2 THEN                          /* 2. decadal       */
       ASSIGN W_NroDias = 10
              W_NroPer  = 36.
    ELSE IF P_Perded EQ 3 THEN                          /* 3. quincenal     */
       ASSIGN W_NroDias = 15
              W_NroPer  = 24.       
    ELSE IF P_Perded EQ 4 THEN                          /* 4. mensual       */
       ASSIGN W_NroDias = 30
              W_NroPer  = 12.
    ELSE IF P_Perded EQ 5 THEN                          /* 5. bimestral     */
       ASSIGN W_NroDias = 60
              W_NroPer  =  6.
    ELSE IF P_Perded EQ 6 THEN                          /* 6. trimestral    */
       ASSIGN W_NroDias = 90
              W_NroPer  =  4.
    ELSE IF P_Perded EQ 7 THEN                          /* 7. cuatrimestral */
       ASSIGN W_NroDias = 120
              W_NroPer  =   3.
    ELSE IF P_Perded EQ 8 THEN                          /* 8. semestral     */
       ASSIGN W_NroDias = 180
              W_NroPer  =   2.
    ELSE IF P_Perded EQ 9 THEN                          /* 9. anual         */
       ASSIGN W_NroDias = 360
              W_NroPer  =   1.

    RUN P-Amortizacion.
  
  PROCEDURE P-Amortizacion:
  /************************************************************************
  ************************************************************************/
    DEFINE VAR Linea    AS CHAR FORMAT "X(120)" INITIAL "".
    DEFINE VAR Raya     AS CHAR INITIAL "-".
    DEFINE VAR W_Cliente AS CHAR FORMAT "X(40)".
    W_Cliente = P_Nit + " - " + P_Nombre.
    

    W_Ubicacion = STRING(W_Agencia,"999") + " - " + W_Nom_Agencia.
    IF (P_TipArc = "A") THEN W_TitP = "Nro. Asesoría:".

    DEFINE FRAME F-Encabezado
      HEADER
      W_Nom_Entidad   AT 28 FORMAT "X(30)"
      "PAGINA:"       AT 70 PAGE-NUMBER FORMAT ">>>9" 
      "AMORTIZACIONES DEL PRESTAMO" AT 28 SKIP(1)
      "---------------------------------------------------------------------------------------------------" AT 1
      "Agencia        :" AT 1
      W_Ubicacion        AT 18
      "Asesor         :" AT 61
      W_NomUsu           AT 78
      "---------------------------------------------------------------------------------------------------" AT 1
      "Nit            :" AT 1
       W_Cliente         AT 18
       W_TitP            AT 1
       P_NroDcto         AT 18
      "Producto       :" AT 61
       P_NomPro          AT 78 
      "Monto          :" AT 1
       W_MonTra          AT 18 FORMAT "X(20)"
      "Tasa           :" AT 61
       W_TasTra          AT 78 FORMAT "X(50)"
      "Plazo          :" AT 1
       P_Plazo           AT 18
      "Cuota          :" AT 61
       W_Anualidad       AT 78 FORMAT "X(20)"
      "Stma.Utilizado :" AT 1
       P_NomStma         AT 18 FORMAT "X(35)"
      "Razón          :" AT 61
       W_RazTra          AT 78 /*FORMAT "X(20)"*/
      "Período Gracia :" AT 1
       P_Gracia          AT 18 FORMAT ">>,>>>" SKIP(1)
      "---------------------------------------------------------------------------------------------------" AT 1       
    /*  "Nro"             AT  3
      "  Fecha"         AT 13
      "     Cuota"      AT 25
      "   Int-P.Gracia" AT 41
      "  Abono Interés" AT 57
      "  Abono Capital" AT 73
      "  Saldo Capital" AT 89
      Linea           AT  1  FORMAT "X(100)"*/
    WITH 1 DOWN WIDTH 132 USE-TEXT FRAME F-Encabezado PAGE-TOP STREAM-IO NO-BOX NO-LABEL.
          
   /* DEFINE FRAME F-Amortizacion
      W_TabAmor.W_Periodo   AT  3  FORMAT ">>>9"
      W_TabAmor.W_Fecha     AT  8  FORMAT "99/99/9999"
      W_TabAmor.W_Cuota     AT 20  FORMAT "->>>,>>>,>>>,>>>,>>9"
      INT_MGracia           AT 
      W_TabAmor.W_AboInt    AT 60  FORMAT "->>>,>>>,>>>,>>>,>>9"
      W_TabAmor.W_AboCap    AT 40  FORMAT "->>>,>>>,>>>,>>>,>>9"
      W_TabAmor.W_SdoCap    AT 80  FORMAT "->>>,>>>,>>>,>>>,>>9" 
    WITH DOWN WIDTH 132 NO-BOX USE-TEXT STREAM-IO NO-LABELS.*/

    DEFINE FRAME F-Detalle1
    WITH DOWN WIDTH 132 NO-BOX USE-TEXT STREAM-IO NO-LABELS.

    DEFINE FRAME F-Detalle2
    WITH DOWN WIDTH 132 NO-BOX USE-TEXT STREAM-IO NO-LABELS.

    DEFINE FRAME F-PiePagina
      HEADER 
        "FECHA:"   AT 2 
         TODAY     AT 10 FORMAT "99/99/9999"
         W_Nom_Agencia AT 28 FORMAT "X(30)"
        "HORA:"    AT 70 STRING(TIME,"HH:MM AM")
    WITH DOWN WIDTH 132 NO-BOX FRAME F-PiePagina PAGE-BOTTOM USE-TEXT STREAM-IO. 

   FIND Usuarios WHERE Usuarios.Usuario EQ W_Usuario
                    AND Usuarios.Agencia EQ W_Agencia NO-LOCK NO-ERROR.
    IF AVAILABLE(Usuarios) THEN
       ASSIGN W_NomUsu = Usuarios.Nombre.

    ASSIGN Linea    = FILL(Raya,120)
           W_MonTra = TRIM(STRING(P_Monto,"->>,>>>,>>>,>>>,>>9")).
    IF P_Sistema <> 8 THEN
       W_RazTra = TRIM(STRING(P_Razon,"->>,>>>,>>>,>>>,>>9.99")).
    ELSE
       W_RazTra = " ".
    W_Anualidad = TRIM(STRING(P_Cuota,"->>,>>>,>>>,>>>,>>9")).
    IF P_TipInt = 1 THEN
       W_TasTra = TRIM(STRING(P_Tasa * 100,">>9.99") + "% " + P_NomPer + " Vencido").
    ELSE
       W_TasTra = TRIM(STRING(P_Tasa * 100,">>9.99") + "% " + P_NomPer + " Anticipado").

    VIEW FRAME F-Encabezado.
    VIEW FRAME F-PiePagina.

    FIND Pro_Creditos WHERE Pro_Creditos.Cod_Credito EQ P_CodPro NO-LOCK NO-ERROR.
    IF AVAILABLE(Pro_Creditos) AND Pro_Creditos.Id_PerGracia AND Dia_Gracia GT 0 THEN
       ASSIGN INT_MGracia = ROUND((((P_Monto * P_Tasa) / W_NroDias) * Dia_Gracia) / P_Plazo,0).

    CREATE W_TabAmor.
    ASSIGN W_TotInt = 0
           W_TotCap = 0
           W_FecTra = P_Fecha
           W_FecIni = P_Fecha
           W_TabAmor.W_AboInt = 0       
           W_TabAmor.W_Cuota  = 0
           W_TabAmor.W_AboCap = 0
           W_TabAmor.W_SdoCap = P_Monto
           W_CuoTra           = P_Cuota
           W_SdoCapTra        = P_Monto.

    IF DAY(W_FecIni) EQ 31 THEN
       W_FecIni = W_FecIni - 1.

    IF P_Perded EQ 2 OR P_Perded EQ 3 THEN
       W_FecTra = W_FecIni.

    DO W_PerTra = 0 TO P_Plazo BY 1:
       IF P_Sistema <> 2 AND W_PerTra > 0 THEN DO:                 
          RUN CambiarFecha.
          W_TabAmor.W_Fecha = W_FecTra.
       END.

       IF P_Sistema = 1 AND P_TotExt > 0 THEN 
          W_VlrExt = 0.

       IF P_Sistema EQ 1 OR P_Sistema EQ 2 THEN
          ASSIGN W_TabAmor.W_Cuota = P_Cuota.

       ELSE IF P_Sistema EQ 3 THEN
          ASSIGN W_TabAmor.W_Cuota = P_Monto * P_Tasa.

       ASSIGN W_TabAmor.W_Periodo = W_PerTra.

       IF (W_PerTra = 0) THEN DO:
          ASSIGN W_TabAmor.W_Cuota   = 0
                 W_TabAmor.W_Fecha   = P_Fecha.
          IF P_TipInt = 2 THEN
             ASSIGN W_TabAmor.W_AboInt = P_Monto * P_Tasa.
       END.
       ELSE DO:  
          IF P_Sistema EQ 1 THEN
             RUN P-AmortCuotaFija.
          ELSE IF P_Sistema EQ 2 THEN DO: 
             W_NroDias = (P_Plazo * W_NroDias).
             RUN CambiarFecha.
             W_TabAmor.W_Fecha = W_FecTra.

             IF P_TipInt = 1 THEN
                ASSIGN W_TabAmor.W_AboInt = P_Cuota - P_Monto
                       W_TotInt           = W_TabAmor.W_AboInt.
             ASSIGN W_TabAmor.W_AboCap = P_Monto
                    W_TabAmor.W_Fecha  = W_FecTra
                    W_TotCap           = P_Monto
                    W_TabAmor.W_SdoCap = 0
                    W_SdoCapTra        = 0.
             IF W_SdoCapTra = 0 THEN
                LEAVE.
          END.         
          ELSE IF P_Sistema EQ 3 THEN DO: 
             ASSIGN W_TabAmor.W_AboInt = P_Monto * P_Tasa.
             IF P_Plazo = W_PerTra THEN DO:
                IF P_TipInt = 1 THEN
                   ASSIGN W_TabAmor.W_Cuota  = W_TabAmor.W_Cuota + P_Monto.
                ELSE
                   ASSIGN W_TabAmor.W_Cuota  = P_Monto
                          W_TabAmor.W_AboInt = 0.
                ASSIGN W_TabAmor.W_AboCap = P_Monto
                       W_TabAmor.W_SdoCap = 0
                       W_SdoCapTra        = 0.
             END.
          END.     
          ELSE IF P_Sistema EQ 4 THEN
               RUN P-AmortCuotaConstante.
          ELSE IF P_Sistema GE 5 AND P_Sistema LE 7 THEN
               RUN P-AmortizacionGradiente.
          ELSE IF P_Sistema EQ 8 THEN
               RUN P-AmortPeriodoGracia.
          ELSE IF P_Sistema EQ 9 THEN DO:
                 ASSIGN  W_TabAmor.W_AboCap = (P_Monto / P_Plazo)
                         W_SdoCapTra        = (W_SdoCapTra - W_TabAmor.W_AboCap)
                         W_TabAmor.W_SdoCap = W_SdoCapTra.

                 IF W_PerTra = 1 AND P_TipInt = 1 THEN
                    ASSIGN W_TabAmor.W_Cuota  = (P_Monto / P_Plazo) + (P_Monto * P_Tasa)
                           W_TabAmor.W_AboInt = (W_TabAmor.W_Cuota - W_TabAmor.W_AboCap).
                 ELSE
                    ASSIGN W_TabAmor.W_Cuota  = (P_Monto / P_Plazo).
           END.
       END.  

       ASSIGN W_TotInt = W_TotInt + W_TabAmor.W_AboInt 
              W_TotGra = W_TotGra + INT_MGracia WHEN W_PerTra NE 0
              W_TotCap = W_TotCap + W_TabAmor.W_AboCap.

       IF W_PerTra < P_Plazo THEN
          CREATE W_TabAmor. 
       I = I + 1.
    END.  

    FOR EACH W_TabAmor:   /* WITH FRAME F-Amortizacion:*/
          RUN Crear_PLan(INPUT W_Periodo,
                         INPUT W_Fecha,
                         INPUT W_Cuota,
                         INPUT W_Abocap,
                         INPUT W_AboInt,
                         INPUT W_SdoCap).
          DISPLAY W_Periodo     /*AT  3*/   FORMAT ">>>9"              LABEL "PdoN"             
                  W_Fecha       /*AT 13*/   FORMAT "99/99/9999"        LABEL "Vencimiento"
                  W_Cuota       /*AT 25*/   FORMAT "->>,>>>,>>>,>>9"   LABEL "Valor Cuota"
                  INT_MGracia   /*AT 41*/   FORMAT "->>,>>>,>>>,>>9"   LABEL "Int-Pdo.Gracia" WHEN W_Periodo GT 0
                  W_AboInt      /*AT 57*/   FORMAT "->>,>>>,>>>,>>9"   LABEL "Abono Interés"
                  W_AboCap      /*AT 73*/   FORMAT "->>,>>>,>>>,>>9"   LABEL "Abono Capital"
                  W_SdoCap      /*AT 89*/   FORMAT "->>,>>>,>>>,>>9"   LABEL "Saldo Capital"
              WITH DOWN WIDTH 132 FRAME F-DetallCU USE-TEXT STREAM-IO NO-LABELS NO-BOX.
    END.   

    IF W_TotInt <> 0 THEN DO:                        
       DISPLAY W_TotGra AT 32 FORMAT "->>,>>>,>>>,>>9"
               W_TotInt AT 48 FORMAT "->>,>>>,>>>,>>9"
               W_TotCap AT 64 FORMAT "->>,>>>,>>>,>>9"
               WITH DOWN WIDTH 132 USE-TEXT STREAM-IO NO-LABELS.

       IF P_Sistema = 1 AND P_TotExt > 0 THEN DO:            
          DISPLAY SKIP(1) Linea AT 3 FORMAT "X(100)" WITH WIDTH 132 USE-TEXT STREAM-IO NO-LABELS.
          DISPLAY "Cuotas Extras:"        AT 3 SKIP(1) WITH WIDTH 132 USE-TEXT STREAM-IO NO-LABELS.
          DISPLAY "        Valor Cuota"   AT 23 
                  "Fecha Vcto"            AT 50
                  "Vlr.Presente"          AT 70 
                  WITH WIDTH 132 USE-TEXT STREAM-IO NO-LABELS.
          ASSIGN W_TvrPte = 0
                 W_VlrExt = 0.
          FOR EACH W_TabExt WHERE W_TabExt.W_Cuota > 0 NO-LOCK:
                 RUN HPDF IN W_ManFin 
                            (INPUT W_TabExt.W_Cuota,P_Tasa,W_TabExt.W_PlazExt,
                             OUTPUT W_VlrPte).
                 DISPLAY  W_TabExt.W_Cuota AT 23 FORMAT ">>>,>>>,>>>,>>>,>>9"
                          W_TabExt.W_Fecha AT 50 FORMAT "99/99/9999" 
                          W_VlrPte         AT 63 FORMAT ">>>,>>>,>>>,>>>,>>9" SKIP
                 WITH FRAME F-Detalle1.
                 ASSIGN W_VlrExt = W_VlrExt + W_TabExt.W_Cuota
                        W_TvrPte = W_TvrPte + W_VlrPte.
          END.         
          DISPLAY SKIP(1) "TOTAL EXTRAS => " AT 3 
                          W_VlrExt AT 23 FORMAT ">>>,>>>,>>>,>>>,>>9" 
                          W_TVrPte AT 63 FORMAT ">>>,>>>,>>>,>>>,>>9" SKIP
                          WITH FRAME F-Detalle2.
       END.
      /* RUN Deduc_Cre. Comentariado Junio 3/05 GAER, Porque Ya no dependen de la Config.de Pro_creditos */  
    END.  
    PAGE.
  END PROCEDURE.

  PROCEDURE P-AmortCuotaConstante:
  /*******************************************************************************
  *******************************************************************************/
    W_TabAmor.W_AboCap = (P_Monto / P_Plazo).
    IF P_TipInt = 1 THEN
       RUN HCCC IN W_ManFin (INPUT P_Monto,INPUT P_Plazo,INPUT W_PerTra,
                            INPUT P_Tasa,OUTPUT W_TabAmor.W_Cuota).
    ELSE
       W_TabAmor.W_Cuota = ((W_SdoCapTra - W_TabAmor.W_AboCap) * P_Tasa) +
                           W_TabAmor.W_AboCap.
    ASSIGN W_TabAmor.W_AboInt = W_TabAmor.W_Cuota  - W_TabAmor.W_AboCap
           W_SdoCapTra        = W_SdoCapTra - W_TabAmor.W_AboCap
           W_TabAmor.W_SdoCap = W_SdoCapTra.
    IF W_PerTra = P_Plazo AND W_SdoCapTra <> 0 THEN DO: 
       IF W_SdoCapTra < 0 THEN 
          W_TabAmor.W_AboInt = W_TabAmor.W_AboInt - ABS(W_SdoCapTra).
       ELSE   
          W_TabAmor.W_AboInt = W_TabAmor.W_AboInt + W_SdoCapTra.
       W_TabAmor.W_SdoCap = 0.
    END.
  END PROCEDURE.     
  
  PROCEDURE P-AmortCuotaFija:
  /************************************************************************
  ************************************************************************/
    DEFINE VAR W_SdoAnt  LIKE Creditos.Monto.
    DEFINE VAR W_Interes LIKE Creditos.Monto.

    IF P_TipInt = 1 AND W_SdoCapTra GT 0 THEN
       W_TabAmor.W_AboInt = (W_SdoCapTra * P_Tasa).
    ELSE IF W_SdoCapTra GT 0 THEN
       W_TabAmor.W_AboInt = ((W_SdoCapTra - P_Cuota) * P_Tasa).

    ASSIGN W_SdoAnt           = W_SdoCapTra
           W_Interes          = W_TabAmor.W_AboInt 
           W_TabAmor.W_Cuota  = P_Cuota + W_VlrExt.

    IF W_SdoCapTra GT 0 THEN 
       ASSIGN W_TabAmor.W_AboCap = W_TabAmor.W_Cuota - (W_TabAmor.W_AboInt + INT_MGracia)
              W_SdoCapTra        = W_SdoCapTra - W_TabAmor.W_AboCap
              W_TabAmor.W_SdoCap = W_SdoCapTra.

    IF W_SdoCapTra LE 0 THEN
       ASSIGN W_TabAmor.W_AboCap = W_TabAmor.W_AboCap + W_SdoCapTra
              W_TabAmor.W_SdoCap = 0
              W_TabAmor.W_Cuota  = W_TabAmor.W_AboInt + INT_MGracia + W_TabAmor.W_AboCap
              W_SdoCapTra        = 0.    
  END PROCEDURE.                         
  
  PROCEDURE P-AmortizacionGradiente:
  /*******************************************************************************
  *******************************************************************************/
    DEFINE VAR W_Dia    AS INTEGER INITIAL 0.
    IF W_PerTra  > 1 AND P_Sistema <> 7 THEN DO:
       IF P_Sistema EQ 5 THEN       
          W_CuoTra  = W_CuoTra + P_Razon.
       ELSE IF P_Sistema EQ 6 THEN
          W_CuoTra  = W_CuoTra * P_Razon.
    END. 
    ASSIGN W_TabAmor.W_Cuota  = W_CuoTra
           W_TabAmor.W_AboInt = W_SdoCapTra * P_Tasa
           W_TabAmor.W_AboCap = W_TabAmor.W_Cuota - W_TabAmor.W_AboInt
           W_SdoCapTra        = W_SdoCapTra - W_TabAmor.W_AboCap
           W_TabAmor.W_SdoCap = W_SdoCapTra.
    
    /*IF W_PerTra = P_Plazo AND W_SdoCapTra <> 0 THEN DO: 
       IF W_SdoCapTra < 0 THEN 
          ASSIGN W_TabAmor.W_AboCap = W_TabAmor.W_AboCap - ABS(W_SdoCapTra)
                 W_TabAmor.W_AboInt = W_TabAmor.W_AboInt + ABS(W_SdoCapTra).
       ELSE ASSIGN W_TabAmor.W_AboCap = W_TabAmor.W_AboCap + W_SdoCapTra
                   W_TabAmor.W_AboInt = W_TabAmor.W_AboInt - W_SdoCapTra.
       W_TabAmor.W_SdoCap = 0.
    END.*/

    IF (P_Sistema = 7) THEN DO:
        IF (P_PerDed = 1 OR P_PerDed = 2 OR P_PerDed = 3) THEN DO:
              W_Dia = ((YEAR(W_FecTra) - YEAR(P_Fecha)) * 360 + 
                      (MONTH(W_FecTra) - MONTH(P_Fecha)) *  30).
              IF W_Dia > 0 AND W_Dia MODULO 360 = 0 AND I MODULO W_NroPer = 0 THEN
                 ASSIGN W_CuoTra  = W_CuoTra * P_Razon.
        END.
        ELSE DO:        
              W_Dia = ((YEAR(W_FecTra) - YEAR(P_Fecha)) * 360 + 
                      (MONTH(W_FecTra) - MONTH(P_Fecha)) *  30).
              IF W_Dia > 0 AND W_Dia MODULO 360 = 0  THEN
                 ASSIGN W_CuoTra  = W_CuoTra * P_Razon.
        END.     
    END.
  END PROCEDURE.
  
  PROCEDURE P-AmortPeriodoGracia:
    IF P_Gracia = W_PerTra THEN DO:
       RUN HFDP IN W_ManFin (INPUT P_Monto,INPUT P_Tasa,INPUT P_Gracia,OUTPUT W_SdoCapTra).
       W_TotInt = W_SdoCapTra - P_Monto.
       P_Monto  = W_SdoCapTra.
       RETURN.
    END.                   
    ELSE IF P_Gracia > W_PerTra THEN
            RETURN.
    W_TabAmor.W_Cuota = P_Cuota.
    RUN HCCF IN W_ManFin (INPUT P_Monto,INPUT P_Cuota,INPUT W_PerTra - P_Gracia,INPUT P_Tasa,
             OUTPUT W_TabAmor.W_AboCap).
    ASSIGN W_TabAmor.W_AboInt = W_TabAmor.W_Cuota  - W_TabAmor.W_AboCap
           W_SdoCapTra        = W_SdoCapTra - W_TabAmor.W_AboCap
           W_TabAmor.W_SdoCap = W_SdoCapTra.
    IF W_PerTra = P_Plazo  AND W_SdoCapTra <> 0 THEN DO: 
       IF W_TabAmor.W_SdoCap < 0 THEN 
          ASSIGN W_TabAmor.W_AboCap = W_TabAmor.W_AboCap - ABS(W_TabAmor.W_SdoCap)
                 W_TabAmor.W_AboInt = W_TabAmor.W_AboInt + ABS(W_TabAmor.W_SdoCap).
       ELSE ASSIGN W_TabAmor.W_AboCap = W_TabAmor.W_AboCap + W_TabAmor.W_SdoCap
                   W_TabAmor.W_AboInt = W_TabAmor.W_AboInt - W_TabAmor.W_SdoCap.
       W_TabAmor.W_SdoCap = 0.
    END.
  END PROCEDURE.
  
  PROCEDURE CambiarFecha: 
  /*--------------------*/
    DEFINE VAR W_Mes    AS INTEGER INITIAL 0.
    DEFINE VAR W_Ind    AS INTEGER INITIAL 0.
    DEFINE VAR W_DiaMes AS INTEGER EXTENT 12 INITIAL 
               [31,28,31,30,31,30,31,31,30,31,30,31].

    IF W_NroDias >= 30 THEN DO:        
       W_Mes = W_NroDias / 30.       
       DO W_Ind = 1 TO W_mes BY 1:                        
          IF MONTH(W_FecTra) = 2 AND YEAR(W_FecTra) MODULO 4 = 0 THEN
             W_FecTra = W_FecTra + 1.
          IF (W_DiaMes[MONTH(P_Fecha)] = DAY(P_Fecha)) 
          OR (MONTH(P_Fecha) = 2 AND DAY(P_Fecha) >= 28) THEN DO: 
             IF MONTH(W_FecTra) + 1 > 12 THEN
                W_FecTra = W_FecTra + W_DiaMes[1].
             ELSE W_FecTra = W_FecTra + W_DiaMes[MONTH(W_FecTra) + 1].
          END.
          ELSE DO:
            IF DAY(P_Fecha) >= 29 AND MONTH(W_FecTra) <= 2 THEN
               IF MONTH(W_FecTra) = 1 THEN
                  W_FecTra = DATE(02,28,(YEAR(W_FecTra))).
               ELSE
                  W_FecTra = DATE(03,DAY(P_Fecha),(YEAR(W_FecTra))).
            ELSE
               W_FecTra = W_FecTra + W_DiaMes[MONTH(W_FecTra)].
          END.     
       END.
    END.
    ELSE DO:   
       W_FecTra = W_FecTra + W_NroDias.

       IF W_NroDias EQ 7 THEN
          RETURN.

       IF MONTH(W_FecTra) EQ 3 AND W_Si28 THEN
          ASSIGN W_FecTra = W_FecTra - 2.
       ELSE IF MONTH(W_FecTra) EQ 3 AND W_Si29 THEN
           ASSIGN W_FecTra = W_FecTra - 1.
         
        ASSIGN W_Si28 = FALSE
               W_Si29 = FALSE.


        IF MONTH(W_FecTra) EQ MONTH(W_FecTra - W_NroDias)  /*En el mismo mes*/
        OR MONTH(W_FecTra) EQ 12
        OR MONTH(W_FecTra) EQ 5
        OR MONTH(W_FecTra) EQ 7
        OR MONTH(W_FecTra) EQ 10 THEN  /*O Mes anterior fue de 30 dias, Ok*/
           RETURN.

        IF MONTH(W_FecTra) EQ 3 AND DAY(W_FecTra) LE 4 THEN DO:    /*Paso de Feb.a Marzo*/
           IF (DAY(W_FecTra) LE 15 AND W_NroDias EQ 15) 
           OR (DAY(W_FecTra) LE 20 AND W_NroDias EQ 10) THEN DO:
              
              W_FecTra = DATE(02,28,YEAR(W_FecTra)).

              IF DAY(W_FecTra + 1) = 29 THEN
                 ASSIGN W_FecTra = W_FecTra + 1
                        W_Si29    = TRUE.
              ELSE
                 W_Si28 = TRUE.
              RETURN.
           END.
           ELSE
              W_FecTra = W_FecTra - 2.

           RETURN.
        END.
        ELSE W_FecTra = W_FecTra + 1.  /*El resto,mes anterior de 31*/

        RETURN.
    END.
  END PROCEDURE.

PROCEDURE Deduc_Cre:
   DEFINE VAR W_TotVal        LIKE Deducible.Valor INITIAL 0.
   DEFINE VAR W_Vdd           LIKE Deducible.Valor.
   DEFINE VAR W_Ndd           AS INTEGER FORMAT "99".
   DEFINE VAR W_Porcent       LIKE Deducible.Valor.
   DEFINE VAR Linea1          AS CHAR FORMAT "X(120)" INITIAL "".
   
   DISPLAY SKIP(2) Linea1 AT 3 FORMAT "X(100)" WITH WIDTH 132 USE-TEXT STREAM-IO NO-LABELS.
   DISPLAY "DEDUCIBLES:"  AT 3 "DESCRIPCION" AT 23 "VALOR DEDUCIBLE    % APLICADO" AT 55 SKIP
   WITH WIDTH 132 USE-TEXT STREAM-IO NO-LABELS.

   ASSIGN W_TotVal       = 0.
   FIND Pro_Creditos WHERE Pro_Creditos.Cod_Credito EQ P_CodPro NO-LOCK NO-ERROR.
   IF AVAILABLE(Pro_Creditos) THEN DO W_Ndd = 1 TO 10:
      IF Pro_Creditos.Deducible[W_Ndd] GT "0000" THEN DO:
         FIND Deducible WHERE Deducible.Cod_Deducible EQ Pro_Creditos.Deducible[W_Ndd] 
                          AND Deducible.Estado        EQ 1 
                          AND Deducible.Tip_Deducible EQ 1 NO-LOCK NO-ERROR.
         IF AVAILABLE(Deducible) THEN DO:
            ASSIGN W_Vdd = Deducible.Valor
                   W_Porcent = 0.
            IF Deducible.Cla_Deducible = 1 THEN DO:
               W_Porcent = Deducible.Valor.
               IF W_IDeduc EQ 4 THEN 
                  ASSIGN W_Vdd = ROUND ((P_VrSoli  * Deducible.Valor) / 100 ,0).
               ELSE ASSIGN W_Vdd = ROUND ((P_VrSoli * Deducible.Valor) / 100 ,0).
            END.
            ASSIGN W_TotVal  = W_TotVal + W_Vdd.
            RUN Imp_Deducibles (INPUT W_Porcent, W_Vdd).
         END.
      END.
   END.
   DISPLAY SKIP(1) "TOTAL DEDUCIBLES => " AT 3 W_TotVal * 100 AT 55 FORMAT ">>>,>>>,>>>,>>9" SKIP
           WITH FRAME F-Detalle5 NO-LABELS.

END PROCEDURE.

PROCEDURE Imp_Deducibles:
  DEFINE INPUT PARAMETER P_Porcent    LIKE Deducible.Valor.
  DEFINE INPUT PARAMETER P_VlrPorc    LIKE Deducible.Valor.
           DISPLAY  Deducible.Nom_Deducible AT 23 FORMAT "X(30)"
                    P_VlrPorc   * 100       AT 55 FORMAT ">>>,>>>,>>>,>>9" 
                    P_Porcent               AT 75 FORMAT "%>>>9.9999" 
                WITH WIDTH 150 FRAME F-Detalle3 NO-LABELS.
END PROCEDURE.

PROCEDURE Crear_Plan:
/*--------------------*/
DEFINE INPUT PARAMETER W_Periodo AS INTEGER FORMAT "99".
DEFINE INPUT PARAMETER W_Fecha AS DATE.
DEFINE INPUT PARAMETER W_Cuota LIKE Creditos.Cuota.
DEFINE INPUT PARAMETER W_Abocap LIKE Creditos.Sdo_Capital.
DEFINE INPUT PARAMETER W_AboInt LIKE Creditos.Sdo_Capital.
DEFINE INPUT PARAMETER W_SdoCap LIKE Creditos.Sdo_Capital.

DEFI VAR W_FecAnt AS DATE.

IF W_Primero THEN 
   W_FecAnt = TODAY.

IF P_TipInt EQ 1 AND W_Periodo EQ 0 THEN RETURN.
CREATE PlanPagos.
ASSIGN PlanPagos.Agencia      = W_Agencia
       PlanPAgos.Nit          = P_Nit
       PlanPagos.Num_Credito  = P_NroDcto
       PlanPagos.Cod_Credito  = P_CodPro
       PlanPagos.Fec_Ini      = W_FecAnt
       PlanPagos.Fec_Vcto     = W_Fecha
       PlanPagos.Fec_ProxPago = W_Fecha
       PlanPagos.Nro_Cuota    = W_Periodo
       PlanPagos.Cuota        = W_Cuota
       PlanPagos.Tasa         = P_Tasa
       PlanPagos.Plazo        = P_Plazo
       PlanPagos.Monto        = P_Monto
       PlanPagos.Id_PdoMes    = 0
       W_FecAnt               = W_Fecha.
IF W_Primero THEN
   ASSIGN PlanPagos.Id_PdoMes    = 1
          PlanPagos.Fec_Ini      = TODAY
          PlanPagos.Fec_ProxPago = W_Fecha
          W_Primero              = NO.


END PROCEDURE.
